#!/usr/bin/perl

use strict;
use vars qw/$query $query_type $ontology $obo $script_url $format %ontology2ace $target/;

use Ace::Browser::AceSubs qw/:DEFAULT/;
use CGI qw/:standard *table *TR *td *div/;

use Ace::Browser::SearchSubs qw/:DEFAULT/;
use ElegansSubs qw/:DEFAULT !TypeSelector/;

use lib 'browser_lib/';
use OBrowse;

use constant ISA    => '/images/ontology/is_a_serif_small.png';
use constant PARTOF => '/images/ontology/part_of_serif_small.png';

# Problems:  not currently displaying the link to fetch annotations as text.

END {
  undef $query;
  undef $query_type;
  undef $ontology;
  undef $script_url;
  undef $obo;
  undef $format;
  undef %ontology2ace;
  undef $target;
}

$script_url = url(-absolute=>1);

# Pull out some parameters
$query      = param('query');
$query_type = param('query_type');
$ontology   = param('ontology');
$format     = param('format');
$target     = '_blank' if $format eq 'inline';

# Map ontologies to their corresponding ace_classes
%ontology2ace = (
		 go => 'GO_term',
		 po => 'Phenotype',
		 ao => 'Anatomy_term');

# Set up the document.
establish_page();

# No ontology or query_type selected?  Just display the form and exit
if ($ontology && $query_type) {
  display_page();
}

display_form() unless ($format =~ /text|inline/);

PrintBottom();
exit;

####################################################################

sub display_page {
  my $socket_path = Configuration->Ontology_socket_path . "/$ontology.socket";
  $obo = OBrowse->new(Peer=>$socket_path,
		      Type=>SOCK_STREAM)
    || die "cannot create object: $!; expected socket at $socket_path; check that the socket server is running\n";
  
  if ($query_type eq 'annotations') {             # retrieve annotations
    my @terms      =split('\|', $query);
    my $linebreak  = br;

    my $ref = $obo->getAnnotations(\@terms,param('not_annotations'))
      || die "cannot get annotations: $!";

    # Query submitted but no annotations found?
    # Display the query form and exit.
    not_found("No annotations found for $query") if (!@{$ref});

    if ($format eq 'text') {
      $linebreak = "\n";
      print scalar @$ref, " annotations retrieved$linebreak";
      foreach (@$ref) {
	print join("\t", @$_), $linebreak;
      }
    } else {
      printAnnotations($ref);
    }
  } elsif ($query =~ /GO:|WBbt:|WBPhenotype/i) {  # ontology term
    if ($query =~ /(GO:)/i) {                     # make it case-insensitive
      $query =~ s/$1/GO:/;
    } elsif ($query =~ /(WBbt:)/i) {
      $query =~ s/$1/WBbt:/;
    } elsif ($query =~ /(WBPhenotype)/i) {
      $query =~ s/$1/WBPhenotype/;
    }
    
    my $propagate_to_root = '';
    my $ref=$obo->browseTerm($query, $propagate_to_root) || die "cannot get term: $!";
    
    # Nothing found? Display a warning and exit
    not_found("$query is not found in " . uc($ontology) . " ontology. Please go back.") if (!%$ref);
    
    printTree($ref);
  } else {                                         # term lookup
    my $ref = $obo->findTerms($query) || die "cannot find terms: $!";
    
    # Nothing found? Display a warning and exit
    not_found("$query is not found in " . uc($ontology) . " ontology. Please go back.") if (!%$ref);
    
    my @aspect          = param('aspect');
    my %aspect_hash = ();
    foreach (@aspect) {
      $aspect_hash{$_} = 1;
    }
    
    my %to_delete=();
    foreach (keys %$ref) {
      if (!$aspect_hash{$$ref{$_}{namespace}} && $ontology eq 'go') {
	$to_delete{$_}=1;
      }
      if ($$ref{$_}{other}{is_obsolete}) {     # skip obsolete terms
	$to_delete{$_}=1;
      }
    }
    foreach (keys %to_delete) {
      delete $$ref{$_};
    }
    printSearchResults($ref);
  }
}

sub not_found {
  my $message = shift;
  if ($format eq 'text') {
    print $message;
  } else {
    unless ($format =~ /inline|text/) {
      print div({-class=>'warn'},$message);
      display_form();
      PrintBottom();
    }
  }
  exit;
}


sub display_form {
  my %ontologies = (
		    go  => 'Gene',
		    po  => 'Phenotype',
		    ao  => 'Anatomy',
		   );
  
  my %aspects  = (
		  biological_process => 'BP',
		  cellular_component => 'CC',
		  molecular_function => 'MF',
		 );

  print hr();
  print h2('Browse ontologies at WormBase') unless (!$query);
  print start_form({-name=>$script_url,-name=>'browse_form'});
  print hidden({-name  => 'query_type',
		-value => 'term_search'}),
		  hidden({-name  => 'details',
			  -value => 'on'}),
			    div({-class=>'form'},
				div({-class=>'row'},
				    span({-class=>'field_name'},'Query:'),
				    span({-class=>'field'},
					 textfield({-name => 'query',
						    -size => '50'}))),
				div({-class=>'row'},
				    span({-class=>'field_name'},'Ontology:'),
				    span({-class=>'field'},
					 popup_menu({-name   => 'ontology',
						     -values => [ qw/ ao po go/ ],
						     -labels => \%ontologies,
						     -default => $ontology || 'go',
						     -onChange=>qq{ var block = document.getElementById('aspect_row'); if (this.value=='go') {block.style.display = 'block'} else { block.style.display='none'}}
						    }))),
				div({-id=>'aspect_row'},
				    span({-class=>'field_name'},'Aspect:'),
				    span({-class=>'field'},
					 checkbox_group({-name   => 'aspect',
							 -values => [ qw/biological_process
									 cellular_component molecular_function/],
							 -labels => \%aspects,
							 -default => [ keys %aspects ]}))),
				div({-class=>'row'},
				    span({-class=>'field'},
					 submit(-value => 'Submit'),
					 '&nbsp;',
					 submit(-value => 'Reset')))),
					   end_form();
}


# Begin the page as text or HTML
sub establish_page {
  # Text or HTML
  if ($format eq 'text') {
    print header(-type=>"text/plain");
  } else {
    unless ($format =~ /inline|text/) {
      # Hacks - PrintTop is choking - shared var problem?
      $Ace::Browser::AceSubs::TOP = undef;
      PrintTop(undef,$ontology2ace{$ontology} || 'GO_term',
	       ($query)
	       ? "WormBase Ontology Browser: $query"
	       : "WormBase Ontology Browser",
	       -style=>{-src=>['/stylesheets/wormbase.css',
			       '/stylesheets/ontology.css'
			      ]});

      $ElegansSubs::PAGE_OPEN++;

      # What should this link be?
      # Originally, it linked back to the browser search itself
      # print div({-id => 'home'},
      #	  a({-href=>"/db/ontology/gene?name=$query"},
      #	    'Back to submission form')) unless !$query;
    }
  }
}


sub printAnnotations {
  my $annotations = shift;

  my $not = param("not_annotations");
  my @fields = ();
  my @cols   = ();

  if ($not && $ontology ne 'go') {
    @cols   = ('Gene Name', 'Qualifier', 'Ontology Term ID', 'Reference', 'Evidence Type', 'Evidence');
    @fields = (3,4,0,6,7,8);
  } elsif ($not && $ontology eq 'go') {
    @cols   = ('Gene Name', 'Qualifier', 'Ontology Term ID', 'Reference', 'Evidence Type');
    @fields = (3,4,0,6,7);
  } elsif (!$not && $ontology ne 'go') {
    @cols   = ('Gene Name', 'Ontology Term ID', 'Reference', 'Evidence Type', 'Evidence');
    @fields = (3,0,6,7,8);
  } elsif (!$not && $ontology eq 'go') {
    @cols   = ('Gene Name', 'Ontology Term ID', 'Reference', 'Evidence Type');
    @fields = (3,0,6,7);
  }

  print span({-id => 'search_results'},scalar @$annotations . ' annotations found'),br,br;
  print start_table({-align=>'center',-border=>1,-width=>'100%'});
  print start_TR();
  foreach (@cols) {
    print td({-class => 'annotations_header'},$_);
  }
  print end_TR();
  foreach my $l (@$annotations) {
    print start_TR();
    for my $i (@fields) {
      if (!$$l[$i]) {
	$$l[$i]='&nbsp';
      }
      my @tmp=split('\|',$$l[$i]); 
      my $class='';
      if ($i==2 || $i==3) {
	$class='gene';
      } elsif ($i==5 || $i==0) {
	$class = $ontology2ace{$ontology};
      } elsif ($i==6) {
	$class='paper';
      } elsif ($i==8) {
	$class=$$l[7] if $$l[7] eq 'Expr_pattern'
	  || $$l[7] eq 'RNAi' || $$l[7] eq 'Variation' || $$l[7] eq 'Transgene';
      }

      print td({-class=>'annotations'});
      foreach (@tmp) {
	if ($class) {
	  if ($class eq 'paper' && /PMID/) {
	    s/PMID://;
	    print a({-href=>Configuration->Pubmed_retrieve . $_},"&nbsp;PMID:$_&nbsp");
	  } else {
	    if (/WB:WBPaper/) {
	      s/WB://;
	    }
	    if (/WB:WP:/) {
	      s/WB:WP://;
	    }
	    if ($i==0) {
	      print a({-href   => "/db/get?name=$$l[5];class=$class",
		       -target => $target},
		      "&nbsp;$_&nbsp;");
	    } else {
	      print a({-href => "/db/get?name=$_;class=$class",
		       -target => $target},"&nbsp;$_&nbsp;");
	    }
	  }
	} else {
	  print qq(&nbsp;$_&nbsp;);
	}
      }
      print end_td();
    }
    print end_TR();
  }
  print end_table();
}



sub printSearchResults {
  my ($ref) = @_;
  my %terms_found_hash = %$ref;

  my ($add_param,
      $details,$siblings,$parent_levels,$children_levels,$expand_tree) = build_params();

  print table({-width => '100%',-align => 'center'},
	      TR(
		 td({-id=>'term_tag',-width=>'20%'},'Query'),
		 td({-id=>'term_tag'},$query)),
	      TR(
		 td({-id=>'term_tag',-width=>'20%'},'Terms found'),
		 td({-id=>'term_data'},scalar keys %terms_found_hash))),br;
  
  my $namespace = '';
  foreach my $id (sort {
    if (($terms_found_hash{$a}{namespace} cmp $terms_found_hash{$b}{namespace}) != 0) {
      return $terms_found_hash{$a}{namespace} cmp $terms_found_hash{$b}{namespace};       # sort by aspect (namespace), then by name
    } else {
      return $terms_found_hash{$a}{name} cmp $terms_found_hash{$b}{name};
    }
  } keys %terms_found_hash) {
      if ($namespace ne $terms_found_hash{$id}{namespace}) {
	if ($namespace) {
	    print end_table(),br;
	}
      $namespace = $terms_found_hash{$id}{namespace};
      print span({-id=>'namespace'},$namespace),br;
      print start_table({-align=>'center',-border=>1,-width=>'100%'}),start_TR();
	foreach ('Term ID', 'Name', 'Description') {
	print td({-class=>'annotations_header'},$_);
      }
      print end_TR();
    }
    print start_TR(),
      td({-class  => 'annotations'},
	 a({-href => "$script_url?query=$id;query_type=term_id;ontology=$ontology$add_param",
	    -target => $target},
	   $id)),
	     td({-class  => 'annotations'},$terms_found_hash{$id}{name});
    if ($terms_found_hash{$id}{other}{def}) {
      print start_td({-class  => 'annotations'});
      foreach (@{$terms_found_hash{$id}{other}{def}}) {
	s/\\//g;
	s/\"//g;
	print "$_\t";
      }
      print end_td();
    } else {
      print td({-class  => 'annotations'},'&nbsp;');
    }
    print end_TR();
  }
  print end_table();
}


# Build a suitable URL (and return parameters to boot).
sub build_params {
    my $add_param;
    my @params;
    #    foreach (qw/details siblings parent_levels children_levels format/) {
    foreach (qw/details siblings parent_levels children_levels/) {
	my $data = param($_);	
	if ($data || ($_ eq 'details' && $format ne 'inline')) {
	  $data ||= 'on';
	  $add_param .= ";$_=$data";
	}
	push @params,$data;
    }
    
    if (!param('expand')) {
	$add_param .= ";expand=1";
    } else {
	push @params,1;
    }
    return $add_param,@params;
}

sub printTree {
  my ($ref) = @_;
  my %data=%$ref;
  
  my ($add_param,
      $details,$siblings,$parent_levels,$children_levels,$format,$expand_tree) = build_params();

  my $tab='&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;';
  foreach my $id (sort {$a cmp $b} keys %data) {
    if (! defined $data{$id}{paths}) {      #top level term
      @{$data{$id}{paths}{1}{proximal_path_parent_child}}=['self_in_path:', '', $id, $data{$id}{name}];
    }
    
    my @tag_names = qw/namespace def synonym exact_synonym broad_synonym
		       narrow_synonym related_synonym subset is_obsolete/;
      my %tag_name_hash = (
			   'namespace'       => 'Namespace',
			   'def'             => 'Definition',
			   'synonym'         => 'Synonym',
			   'exact_synonym'   => 'Exact synonym',
			   'broad_synonym'   => 'Broad synonym',
			   'narrow_synonym'  => 'Narrow synonym',
			   'related_synonym' => 'Related synonym',
			   'subset'          => 'Subset',
			   'is_obsolete'     => 'Obsolete');

    if ($details) {
      print h3('Term Details');
      print start_div({-class=>'term_details'});
      print start_table({-width=>'100%',-align=>'center'});
      print TR(td({-id => 'term_tag'},'Term Name'),
	       td({-id => 'term_data'},$data{$id}{name}));
      print TR(td({-id => 'term_tag'},'Term ID'),
	       td({-id => 'term_data'},a({-href=>"/db/get?name=$id;class=$ontology2ace{$ontology}",
					  -target => $target},
					 ,$id)));
      foreach my $l (@tag_names) {
	if ($data{$id}{other}{$l}) {
	  my $tag_name=$tag_name_hash{$l};
	  foreach (@{$data{$id}{other}{$l}}) {
	    my $tmp=$_;
	    $tmp=~s/\\//g;
	    $tmp=~s/\"//g;
	    if ($tmp=~/\[.*\]/) {
	      $tmp=~s/$1//;
	    }
	    print TR(td({-id => 'term_tag'},$tag_name),
		     td({-id => 'term_data'},$tmp));
	    $tag_name = '';
	  }
	}
      }
      print TR(td({-id => 'term_tag'},
		  'Number of paths to term'),
	       td({-id => 'term_data'},$data{$id}{path_count}));
      print end_table();
      print end_div();
      print hr({-width=>'98%'});
    }

    foreach my $p (sort {$a <=> $b} keys %{$data{$id}{paths}}) {
      my $offset='';
      print div({-id => 'path_number'},"Path $p");

      my $i=0;	
      my $first_parent=0;
      if ($data{$id}{paths}{$p}{path_parent}) {
	if ((scalar @{$data{$id}{paths}{$p}{path_parent}} - $parent_levels) 
	    > 0 && defined $parent_levels) {
	  $first_parent=scalar @{$data{$id}{paths}{$p}{path_parent}} - $parent_levels;
	}
      }
      foreach (@{$data{$id}{paths}{$p}{path_parent}}) {
	if ($i >= $first_parent) {
	  $offset.=$tab;
	  print "$offset";
	  if ($$_[0] eq 'is_a') {
	    print_isa_img() unless $i == 0;
	  } elsif ($$_[0] eq 'part_of') {
	    print_partof_img() unless $i == 0;
	  }
	  print a({-id   => 'ontology_link',
		   -href => "$script_url?query=$$_[1];query_type=term_id;ontology=$ontology$add_param",
		   -target => $target},
		  $$_[2]),br;   # THIS HAD TRAILING NEWLINE - text output?
	}
	$i++;
      }
      $offset.=$tab;
      
      foreach (@{$data{$id}{paths}{$p}{proximal_path_parent_child}}) {
	my $expand='';
	$expand='+' if $$_[3];
	if ($$_[0] eq 'is_a' && $siblings) {
	  print $offset;
	  print_isa_img();
	  print a({-id   => 'ontology_link',
		   -href => "$script_url?query=$$_[1];query_type=term_id;ontology=$ontology$add_param",
		   -target => $target},
		  $$_[2]),"&nbsp;$expand",br;   # THIS HAD TRAILING NEWLINE - text output?
	} elsif ($$_[0] eq 'part_of' && $siblings) {
	  print $offset;
	  print_partof_img();
	  print a({-id   => 'ontology_link',
		   -href => "$script_url?query=$$_[1];query_type=term_id;ontology=$ontology$add_param",
		   -target => $target},
		  $$_[2]),"&nbsp;$expand",br;   # THIS HAD TRAILING NEWLINE - text output?
	} elsif($$_[0] eq 'self_in_path:') {
	  print $offset;
	  print_isa_img()    if ($$_[1] eq 'is_a');
	  print_partof_img() if ($$_[1] eq 'part_of');
	  print span({-id=>'ontology_link_self'},
		     $$_[3] . ':');
	  
	  if ($data{$id}{tac}{combined}) {
	    print span({-id =>"annotations_link"},
		       '&nbsp;&nbsp;'
		       . a({-href => "$script_url?query=$data{$id}{taoc}{terms};query_type=annotations;ontology=$ontology",
			    -target => $target},
			   "genes $data{$id}{taoc}{count}, annotations $data{$id}{tac}{combined}"));
	    print span({-id => "annotations_link_text"},
		       '&nbsp;'
		       . a({-href => "$script_url?query=$data{$id}{taoc}{terms};query_type=annotations;ontology=$ontology;format=text",
			    -target => $target},
			   '(text)'));   # Had a trailing newline
	  } else {
	    print span({-id => "annotations_link"},
		       '&nbsp;&nbsp;'
		       . "genes $data{$id}{taoc}{count}, annotations $data{$id}{tac}{combined}");  # Had a trailing newline
	  }
	  if ($data{$id}{tac_not}{combined}) {
	    print span({-id => 'annotations_link'},
		       '&nbsp;&nbsp;&nbsp;&nbsp;'
		       . span({-id => "standout"},
			      'NOT:&nbsp;&nbsp;')
		       . a({-href => "$script_url?query=$data{$id}{taoc_not}{terms};query_type=annotations;ontology=$ontology&not_annotations=on",
			    -target => $target},
			   "genes $data{$id}{taoc_not}{count}, annotations $data{$id}{tac_not}{combined}"));
	    print span({-id => "annotations_link_text"},
		       '&nbsp;'
		       . a({-href => "$script_url?query=$data{$id}{taoc_not}{terms};query_type=annotations;ontology=$ontology;format=text;not_annotations=on",
			    -target => $target},
			   '(text)'));  # TRAILING NEWLINE
	  } else {
	    #			print qq(<span id="annotations_link">&nbsp;&nbsp;&nbsp;&nbsp;<span id="standout">NOT $$_[3]:&nbsp;&nbsp;</span>genes $data{$id}{taoc_not}{count}, annotations $data{$id}{tac_not}{combined}</span>\n);   # do not print those for now
	  }
	  print br;

	  if ($expand_tree) {
	    print "$offset$tab$tab"
	      . span({-id => 'expand'},
		     a({-href => "$script_url?query=$$_[2];query_type=term_id;ontology=$ontology$add_param",
			-target => $target},
		       'collapse children')) . br
			 if defined @{$data{$id}{children}};   # TRAILING NEWLINE
	    my $i=0;
	    foreach (@{$data{$id}{children}}) {
	      last if $i == $children_levels && $children_levels;
	      $i++;
	      my $obj_count='';
	      if ($$_[2]) {
		$obj_count="($$_[2] genes)";
	      }
	      print "$offset$tab$tab";
	      print a({-id  => 'ontology_link',
		       href => "$script_url?query=$$_[0];query_type=term_id;ontology=$ontology$add_param",
		       -target => $target},
		      $$_[1])
		. span({-id => "obj_count"},"&nbsp;&nbsp;$obj_count") . br;  # Trailing newline
	    }
	  } else {
	    print "$offset$tab$tab"
	      . span({-id => 'expand'},
		     a({-href => "$script_url?query=$$_[2];query_type=term_id;ontology=$ontology$add_param",
			-target => $target},
		       'expand children')) . br
			 if defined @{$data{$id}{immediate_children}}
			   && $#{$data{$id}{children}} > $#{$data{$id}{immediate_children}};  # TRAILING NEWLINE
	    
	    my $i=0;
	    foreach (@{$data{$id}{immediate_children}}) {
	      last if $i == $children_levels && $children_levels;
	      $i++;
	      my $expand='';
	      $expand='+' if $$_[3];
	      print "$offset$tab";
	      print_isa_img()    if ($$_[0] eq 'is_a');
	      print_partof_img() if ($$_[0] eq 'part_of');
	      
	      print a({-id   => 'ontology_link',
		       -href => "$script_url?query=$$_[1];query_type=term_id;ontology=$ontology$add_param",
		       -target => $target},
		      $$_[2])
		.
		  "&nbsp;$expand" . br;   # TRAILING NEWLINE
	    }
	  }
	  next;
	}
      }
      print br;
      print hr({-width => '98%'});
    }
  }
}



# BOTH OF THESE HAD TRAILING NEW LINES
sub print_isa_img {
  print img({-id  => 'relationship',
	     -src => ISA,
	     -border => 0,
	     -height => 15,
	     -alt => 'is-a'});
}


sub print_partof_img {
  print img({-src => PARTOF,
	     -border => 0,
	     -height => '15',
	     -alt    => 'part-of'});
}
